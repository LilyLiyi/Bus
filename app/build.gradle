apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())+100
    }
    catch (ignored) {
        println "===================error code!"
        return 100;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }

        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "v1.0.0";
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.scrat.app.bus"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()
        manifestPlaceholders = [CHANNEL_NAME: "Unspecified"]
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {
            }
        }
        debug{
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {

            }
        }
    }

    dataBinding {
        enabled = true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--abbrev=0', '--tags'
                standardOutput = stdout
            }

            def vn = stdout.toString().trim()
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + vn + ".apk"))
        }
    }

    productFlavors {
        gp {
            manifestPlaceholders = [CHANNEL_NAME: "gp"]
        }
        xiaomi {
            manifestPlaceholders = [CHANNEL_NAME: "xiaomi"]
        }
        baidu {
            manifestPlaceholders = [CHANNEL_NAME: "baidu"]
        }
        mumayi {
            manifestPlaceholders = [CHANNEL_NAME: "mumayi"]
        }
        _91 {
            manifestPlaceholders = [CHANNEL_NAME: "_91"]
        }
        anzhuo {
            manifestPlaceholders = [CHANNEL_NAME: "anzhuo"]
        }
    }

}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'

    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile files('libs/MiPush_SDK_Client_2_2_21.jar')
    compile 'org.jsoup:jsoup:1.9.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}
